#!/bin/awk -f
# Overclock an AMD GPU in Linux from either
# a system-wide *.conf file,
# a user-specified file,
# or STDIN.

# TODO:
# * Error handling for invalid card and power pathnames.
# * Reset TDP functionality.
# * Output current values in suitable format for input.

BEGIN {
  conf = "/usr/local/etc/amdgpuoc.conf"
  card = "/sys/class/drm/card0/device"
  file["clk"] = card "/pp_od_clk_voltage"
  file["tdp"] = find_power(card)
  patt["clk"] = "^r$|^[ms] [0-9]+ [0-9]+ [0-9]+$" # {n,m} not always supported.
  patt["tdp"] = "^[0-9]+$"

  if (ARGC == 1) {
    ARGC++
    ARGV[1] = conf
  }
}

{
  gsub(/[[:space:]]+/, " ")
  gsub(/ *#.*|^ | $/, "")
}

{
  for (i in patt) {
    if ($0 ~ patt[i]) {
      print > file[i]
      close(file[i])
    }
  }
}

/^[/]/ {
  card = $0
  file["clk"] = card "/pp_od_clk_voltage"
  file["tdp"] = find_power(card)
}

/^p$/ {
  for (i in file) {
    print file[i]
    while ((getline line < file[i]) == 1) {
      print line
    }
    close(file[i])
  }
}

END {
  print "c" > file["clk"]
  close(file["clk"])
}

function find_power(path, _cmd, _str) {
  gsub(/"/, "\\&", path)
  _cmd = "find -H \"" path "\" -name power1_cap"
  _cmd | getline _str
  close(_cmd)
  return _str
}
