#!/bin/awk -f
# Overclock an AMD GPU in Linux from either a system-wide .conf file, a user-
# provided file, or from STDIN.

BEGIN {
  conf = "/usr/local/etc/amdgpuoc.conf"
  sys = "/sys/class/drm/card0/device"
  file["clk"] = sys "/pp_od_clk_voltage"
  file["tdp"] = sys "/hwmon/hwmon0/power1_cap"
  patt["clk"] = "^r$|^[ms] [0-9]+ [0-9]+ [0-9]+$" # {n,m} not always supported.
  patt["tdp"] = "^[0-9]+$"

  # If no arguments given, use system-wide .conf file.
  if (ARGC == 1) {
    ARGC++
    ARGV[1] = conf
  }
}

{
  gsub(/[[:space:]]+/, " ")
  gsub(/#.*|^ | $/, "")
  $0 = tolower($0)
  
  # Parse and print input to sysfs files.
  for (i in patt) {
    if ($0 ~ patt[i]) {
      print > file[i]
      close(file[i])
    }
  }

  # Print current values to STDOUT.
  if ($0 ~ "p") {
    for (i in file) {
      print file[i]

      while (getline < file[i] > 0) {
        print
      }

      close(file[i])
    }
  }
}

END {
  # Finalize changes.
  print "c" > file["clk"]
}
